apply plugin: 'com.android.library'

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
    }
}

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    defaultConfig {
        targetSdkVersion 32
        versionCode 15
        versionName "5.0"
        vectorDrawables.useSupportLibrary = true

        // Set dev remote to true to use the remote debugger (running python remotely)
        // must run `enaml-native start --remote-debug` then run your app locally
        buildConfigField "boolean", "DEV_REMOTE_DEBUG", "false"

        // Put your system ip here
        buildConfigField "String", "DEV_SERVER", "\"ws://localhost:8888/dev\""
    }
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

}

dependencies {
    implementation fileTree(include: ['*.jar', '*.so'], dir: 'libs')
    testImplementation 'junit:junit:5.8'

    // Support
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'androidx.activity:activity:1.4.0'
    api "androidx.cardview:cardview:1.0.0"
    api "androidx.drawerlayout:drawerlayout:1.1.1"
    api "androidx.fragment:fragment:1.4.1"
    api "androidx.recyclerview:recyclerview:1.2.1"
    // For control over item selection of both touch and mouse driven selection
    api "androidx.recyclerview:recyclerview-selection:1.1.0"
    api "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    api 'com.google.android.material:material:1.5.0'
    api 'androidx.viewpager:viewpager:1.0.0'
    api 'androidx.browser:browser:1.4.0'
    api "androidx.navigation:navigation-fragment:2.4.1"
    api "androidx.navigation:navigation-ui:2.4.1"

    // Requirements
    api 'org.msgpack:msgpack-core:0.9.+'
    api 'org.kamranzafar:jtar:2.3'

    // Images
    api 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    api "com.github.bumptech.glide:okhttp3-integration:4.9.+"

    // HTTP
    api 'com.squareup.okhttp3:okhttp:4.+'

    // Flexbox
    //compile 'com.facebook.yoga:yoga:1.5.0'
    api 'com.google.android.flexbox:flexbox:3.0.0'

    // Transforms
    api 'com.eftimoff:android-viewpager-transformers:1.0.1@aar'

}

afterEvaluate {

    task bundlePython(type: Exec) {
        workingDir "../../../" // root enaml-native dir
        // Needed to ensure the env is correct by setting the prefix
        environment CONDA_PREFIX: workingDir.path + "/venv/"
        if (project.hasProperty("preReleaseBuild")) {
            commandLine "venv/bin/enaml-native", "bundle-assets", "--release"
        } else {
            commandLine "venv/bin/enaml-native", "bundle-assets"
        }

    }
    preBuild.dependsOn bundlePython
}

